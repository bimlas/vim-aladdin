Describe group

  Before
    call ResetSettings()
  End

  Describe #Register
    It registers user defined lighter
      let g:high_lighters = {'custom': {'hlgroup': 'Normal'}}
      call high#group#Register('custom')
      Assert Equals(high#group#GetSettings('custom')['hlgroup'], 'Normal')
    End

    It registers autoloaded lighter
      call high#group#Register('mixed_eol')
      Assert Equals(high#group#GetSettings('mixed_eol')['pattern'], '\r')
    End

    It registers passed settings as a new lighter
      call high#group#Register('passed', {'hlgroup': 'Normal'})
      Assert Equals(high#group#GetSettings('passed')['hlgroup'], 'Normal')
    End

    It fails if lighter settings are not defined at all
      Throws /\[high\] No such group: not_exists/ high#group#Register('not_exists')
    End

    It applies customization
      let g:high_lighters = {'mixed_eol': {'hlgroup': 'Normal'}}
      call high#group#Register('mixed_eol')
      Assert Equals(high#group#GetSettings('mixed_eol')['hlgroup'], 'Normal')
    End
  End

  Describe #Init
    It calls the init function of autoloaded groups
      call high#group#Register('mixed_eol')
      Assert Empty(high#group#GetMembers('mixed_eol'))
      call high#group#Init('mixed_eol')
      Assert NotEmpty(high#group#GetMembers('mixed_eol'))
    End
  End

  Describe #IsEnabledForFiletype
    Before
      let group = high#group#Register('group', {})
    End

    It enabled for every filetype by default
      let group.whitelist = []
      let group.blacklist = []
      Assert Equals(high#group#IsEnabledForFiletype(group, 'text'), 1)
      Assert Equals(high#group#IsEnabledForFiletype(group, 'asciidoc'), 1)
    End

    It enabled only for whitelisted filetypes
      let group.whitelist = ['text']
      let group.blacklist = []
      Assert Equals(high#group#IsEnabledForFiletype(group, 'text'), 1)
      Assert Equals(high#group#IsEnabledForFiletype(group, 'asciidoc'), 0)
    End

    It disabled only for blacklisted filetypes
      let group.whitelist = []
      let group.blacklist = ['text']
      Assert Equals(high#group#IsEnabledForFiletype(group, 'text'), 0)
      Assert Equals(high#group#IsEnabledForFiletype(group, 'asciidoc'), 1)
    End

    It disabled for both blacklisted and whitelisted filetypes
      let group.whitelist = ['text']
      let group.blacklist = ['text']
      Assert Equals(high#group#IsEnabledForFiletype(group, 'text'), 0)
    End
  End

End
